buildscript {
    ext {
        springBootVersion = '2.4.9'
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://jitpack.io"
        }
    }
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.0.11")
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id "info.solidsoft.pitest" version '1.3.0'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'org.owasp.dependencycheck' version '5.3.0'
    id 'org.sonarqube' version '2.8'
    id "org.flywaydb.flyway" version "6.2.0"
    id 'maven-publish'
    id 'maven'
    id 'uk.gov.hmcts.java' version '0.12.0'
}

apply plugin: 'java'
apply plugin: 'net.serenity-bdd.aggregator'
apply plugin: 'project-report'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'

def versions = [
        lombok             : '1.18.6',
        gradlePitest       : '1.3.0',
        pitest             : '1.4.2',
        serenity           : '2.0.23',
        sonarPitest        : '0.5',
        springBoot         : '2.4.9',
        springHystrix      : '2.1.1.RELEASE',
        springfoxSwagger   : '2.9.2',
        camel              : '3.8.0',
        jackson            : '2.11.4'
]

mainClassName = 'uk.gov.hmcts.reform.*'
def jarName = 'data-ingestion-lib.jar'
def gitRepo = "https://github.com/hmcts/data-ingestion-lib"

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/data-ingestion-lib"
    }
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group = 'uk.gov.hmcts.reform'
version = buildNumber

task sourcesJar(type: Jar, dependsOn: classes) {
    getArchiveClassifier().set('sources')
    from sourceSets.main.allSource
}


artifacts {
    archives sourcesJar
    //archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            //artifact javadocJar
            groupId project.group
            artifactId 'data-ingestion-lib'
            version project.version
        }
    }
}


compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation' << "-Xlint:unchecked" << "-Werror"
}

sourceSets {

    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integrationTest/java')
        }
        resources.srcDir file('src/integrationTest/resources')
    }

    functionalTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/functionalTest/java')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/smokeTest/java')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
}

idea {
    module {
        testSourceDirs += project.sourceSets.integrationTest.java.srcDirs
        testSourceDirs += project.sourceSets.integrationTest.resources.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.java.srcDirs
        testSourceDirs += project.sourceSets.functionalTest.resources.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.java.srcDirs
        testSourceDirs += project.sourceSets.smokeTest.resources.srcDirs
    }
}

// Fix for CVE-2021-21295 & need to be removed with new Azure blob version
configurations.all {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'io.netty') {
            details.useVersion "4.1.63.Final"
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
    functionalTestCompile.extendsFrom testCompile
    functionalTestRuntime.extendsFrom testRuntime

}
check.dependsOn jacocoTestCoverageVerification

pmd {
    toolVersion = '6.12.0'
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test, sourceSets.integrationTest, sourceSets.functionalTest, sourceSets.smokeTest]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSets = [
            'category/java/errorprone.xml',
            'category/java/bestpractices.xml'
    ]
}

task integration(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {

    systemProperty "scenario", System.getProperty('scenario')

    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    finalizedBy aggregate
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    testClassesDirs = sourceSets.smokeTest.output.classesDirs
    classpath = sourceSets.smokeTest.runtimeClasspath
}

task migratePostgresDatabase(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
    baselineOnMigrate = true
    if (project.hasProperty("dburl")) {
        url = "jdbc:postgresql://${dburl}"
    }
}

jacoco {
    toolVersion = "0.8.5"
}

jacocoTestReport {
    executionData(test, integration)
    reports {
        xml.enabled = true
        csv.enabled = false
        xml.destination = file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}


pitest {
    targetClasses = ['uk.gov.hmcts.reform.juddata.*']
    testSourceSets = [sourceSets.test, sourceSets.integrationTest]
    excludedClasses = [
            'uk.gov.hmcts.reform.juddata.configuration.*',
            'uk.gov.hmcts.reform.juddata.JudicialApplication',
            'uk.gov.hmcts.reform.juddata.camel.service.EmailData',
            'uk.gov.hmcts.reform.juddata.camel.service.EmailService'
    ]
    threads = 10
    enableDefaultIncrementalAnalysis = true
    historyInputLocation = ['build/reports/pitest/fastermutationtesting']
    historyOutputLocation = ['build/reports/pitest/fastermutationtestingoutput']
    outputFormats = ['XML', 'HTML']
    timestampedReports = true
    mutationThreshold = 90
    useClasspathFile = true
}


project.tasks['pitest'].group = "Verification"

sonarqube {
    properties {
        property "sonar.projectName", "RD :: Judicial Reference Data load"
        property "sonar.projectKey", "JUDDATA"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.jacoco.itReportPath", "${project.buildDir}/jacoco/integration.exec"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"

        property "sonar.exclusions", "src/main/java/uk/gov/hmcts/reform/juddata/configuration/**," +
                "src/main/java/uk/gov/hmcts/reform/juddata/JudicialApplication.java"
    }
}

project.tasks['sonarqube'].dependsOn test, integration, jacocoTestReport
project.tasks['sonarqube'].group = "Verification"

repositories {
    mavenLocal()

    maven {
        url "https://jitpack.io/"
    }

    mavenCentral()
}

dependencies {
    compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jdbc'

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-cache', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-mail', version: versions.springBoot

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-batch', version: versions.springBoot

    compile group: 'com.github.ben-manes.caffeine', name: 'caffeine', version: '3.0.4'
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson) {
        force = true
    }
    compile (group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson) {
        force = true
    }
    compile group: 'io.github.openfeign.form', name: 'feign-form', version: '3.8.0'
    compile group: 'io.github.openfeign.form', name: 'feign-form-spring', version: '3.8.0'

    compile group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '2.3.3'

    implementation 'com.github.hmcts.java-logging:logging:5.1.7'
    implementation 'com.github.hmcts.java-logging:logging-appinsights:5.1.7'
    implementation 'com.github.hmcts:properties-volume-spring-boot-starter:0.1.0'
    implementation 'com.github.hmcts:service-auth-provider-java-client:4.0.3'
    implementation 'net.logstash.logback:logstash-logback-encoder:6.4'

    compile group: 'com.microsoft.azure', name: 'azure-storage-blob', version: '11.0.0'

    compile group: 'org.flywaydb', name: 'flyway-core', version: '5.2.4'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.2.14'

    compile group: 'com.google.guava', name: 'guava', version: '30.1-jre'

    compile group: 'org.apache.commons', name: 'commons-vfs2', version: '2.4.1'
    compile group: 'com.jcraft', name: 'jsch', version: '0.1.55'

    compile group: 'org.apache.camel.springboot', name: 'camel-spring-boot-starter', version:versions.camel
    compile group: 'org.apache.camel', name: 'camel-csv', version: versions.camel
    compile group: 'org.apache.camel', name: 'camel-sql', version: versions.camel
    compile group: 'org.apache.camel', name: 'camel-spring-batch', version: versions.camel


    compile group: 'org.apache.camel.springboot', name: 'camel-azure-starter', version:versions.camel
    compile group: 'org.apache.camel.springboot', name: 'camel-bindy-starter', version:versions.camel
    compile group: 'org.apache.camel.springboot', name: 'camel-jdbc-starter', version:versions.camel
    compile group: 'org.apache.camel', name: 'camel-bean-validator', version: versions.camel
    compile group: 'org.apache.camel.springboot', name: 'camel-spring-boot-starter', version: versions.camel

    compile group: 'org.apache.camel', name: 'camel-core', version: versions.camel
    compile group: 'org.apache.camel', name: 'spi-annotations', version: '3.4.1'

    compile group: 'com.opencsv', name: 'opencsv', version: '3.7'
    compile group: 'commons-lang', name: 'commons-lang', version: '2.6'
    implementation 'com.sendgrid:sendgrid-java:4.7.2'

    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    annotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    functionalTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestCompileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    smokeTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    testCompile (group: 'io.rest-assured', name: 'rest-assured', version: '4.3.1') {
        exclude group: "com.sun.xml.bind", module: "jaxb-osgi"
    }
    testCompile("org.hamcrest:hamcrest-junit:2.0.0.0") {
        exclude group: "org.hamcrest", module: "hamcrest-core"
        exclude group: "org.hamcrest", module: "hamcrest-library"
    }

    testCompile group: 'com.h2database', name: 'h2'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '3.4.6'
    testCompile group: 'org.mockito', name: 'mockito-inline', version: '3.4.6'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.9'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot
    testCompile group: 'org.springframework.batch', name: 'spring-batch-test', version: '4.2.4.RELEASE'

    testCompile group: 'org.pitest', name: 'pitest', version: versions.pitest
    testCompile 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.3.0'
    testCompile 'org.codehaus.sonar-plugins:sonar-pitest-plugin:0.5'


    testCompile group: 'org.apache.camel', name: 'camel-test-spring', version: versions.camel
    testCompile group: 'org.apache.camel', name: 'camel-test', version: versions.camel
    testCompile group: 'org.testcontainers', name: 'postgresql', version: '1.12.5'
    testCompile group: 'org.postgresql', name: 'postgresql', version: '42.2.8'

    integrationTestCompile group: 'org.apache.camel', name: 'camel-test-spring', version: versions.camel
    integrationTestCompile group: 'org.apache.camel', name: 'camel-test', version: versions.camel
    integrationTestCompile group: 'org.testcontainers', name: 'postgresql', version: '1.12.5'
    integrationTestCompile group: 'org.postgresql', name: 'postgresql', version: '42.2.8'


    testCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.23') {
        force = true
    }

    integrationTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.23') {
        force = true
    }

    functionalTestCompile(group: 'org.yaml', name: 'snakeyaml', version: '1.23') {
        force = true
    }

    integrationTestCompile sourceSets.main.runtimeClasspath
    integrationTestCompile sourceSets.test.runtimeClasspath

    functionalTestCompile sourceSets.main.runtimeClasspath
    functionalTestCompile sourceSets.test.runtimeClasspath

    smokeTestCompile sourceSets.main.runtimeClasspath
    smokeTestCompile sourceSets.test.runtimeClasspath
}

dependencyCheck {
    suppressionFile = 'config/owasp/suppressions.xml'
}

dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

gradle.startParameter.continueOnFailure = true


bootJar {
    enabled = false
}

jar {
    enabled = true
}


// this is required to force Java running on the Azure Windows Server OS into using
// UTF-8 as the default character set instead of windows-1252 which causes issues.
// this setting only applies when running via gradle bootRun -- see web.config for the
// java flag that configures the deployed applications
applicationDefaultJvmArgs = ["-Dfile.encoding=UTF-8"]

test {
    useJUnitPlatform()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(11))
    }
}